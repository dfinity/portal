---
keywords: [advanced, bitcoin, btc, transaction, utxo]
---

import Tabs from "@theme/Tabs";
import TabItem from "@theme/TabItem";
import useBaseUrl from "@docusaurus/useBaseUrl";
import { MarkdownChipRow } from "/src/components/Chip/MarkdownChipRow";

# Creating Bitcoin transactions

<MarkdownChipRow labels={["Advanced", "Bitcoin"]} />

## Overview

A Bitcoin transaction spends a number of unspent transaction outputs (UTXOs) and
creates new UTXOs. In order to create a Bitcoin transaction, you need to:

1. Get the available UTXOs corresponding to a Bitcoin address controlled by the
   canister using the `bitcoin_get_utxos` API endpoint.
2. Calculate an appropriate transaction fee using the `bitcoin_estimate_fee` API
   endpoint.
3. Select a subset of the available UTXOs to spend that covers the transaction
   amount and fee.
4. Create a transaction that spends the selected UTXOs and creates new UTXOs. At
   least one for the recipient, and in most cases, one to collect the change.

## Get available UTXOs

The following snippet shows how to get the available UTXOs corresponding to
`own_address`. Note that a canister can control many addresses and each of them
can have multiple UTXOs associated with it.

<Tabs groupId="language">
<TabItem value="motoko" label="Motoko" default>

```motoko

let own_utxos = (await BitcoinApi.get_utxos(network, own_address)).utxos;

```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/motoko/basic_bitcoin/src/basic_bitcoin/src/BitcoinWallet.mo#L49">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
<TabItem value="rust" label="Rust">

```rust

let own_utxos = bitcoin_api::get_utxos(network, own_address.clone())
    .await
    .utxos;

```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/rust/basic_bitcoin/src/basic_bitcoin/src/bitcoin_wallet.rs#L25">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
</Tabs>

An UTXO has the following structure:

<Tabs groupId="language">
<TabItem value="motoko" label="Motoko" default>

```motoko

/// An unspent transaction output.
public type Utxo = {
    outpoint : OutPoint;
    value : Satoshi;
    height : Nat32;
};

/// A reference to a transaction output.
public type OutPoint = {
    txid : Blob;
    vout : Nat32;
};


```

</TabItem>
<TabItem value="rust" label="Rust">

```rust

/// Unspent transaction output (UTXO).
pub struct Utxo {
    /// See [Outpoint].
    pub outpoint: Outpoint,
    /// Value in the units of satoshi.
    pub value: Satoshi,
    /// Height in the chain.
    pub height: u32,
}

/// Identifier of [Utxo].
pub struct Outpoint {
    /// Transaction Identifier.
    pub txid: Vec<u8>,
    /// A implicit index number.
    pub vout: u32,
}

```

</TabItem>
</Tabs>

To create a transaction that sends `X` satoshis to a destination address, you
need to select a subset of the available UTXOs that cover the amount `X` plus the
transaction fee.

## Calculate transaction fee per byte

The transaction fee of a Bitcoin transaction is calculated based on the size of
the transaction in bytes. An appropriate fee per byte can be determined by
looking at the fees of recent transactions that were included in the blockchain.
The following snippet shows how to estimate the fee per byte for a transaction
using the `bitcoin_get_current_fee_percentiles` API endpoint and choosing the
50th percentile.

<Tabs groupId="language">
<TabItem value="motoko" label="Motoko" default>

```motoko

// Get fee percentiles from previous transactions to estimate our own fee.
let fee_percentiles = await BitcoinApi.get_current_fee_percentiles(network);

let fee_per_vbyte : MillisatoshiPerVByte = if(fee_percentiles.size() == 0) {
    // There are no fee percentiles. This case can only happen on a regtest
    // network where there are no non-coinbase transactions. In this case,
    // we use a default of 1000 millisatoshis/vbyte (i.e. 2 satoshi/byte)
    2000
} else {
    // Choose the 50th percentile for sending fees.
    fee_percentiles[50]
};

```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/motoko/basic_bitcoin/src/basic_bitcoin/src/BitcoinWallet.mo#L61">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
<TabItem value="rust" label="Rust">

```rust

// Get fee percentiles from previous transactions to estimate our own fee.
let fee_percentiles = bitcoin_api::get_current_fee_percentiles(network).await;

let fee_per_byte = if fee_percentiles.is_empty() {
    // There are no fee percentiles. This case can only happen on a regtest
    // network where there are no non-coinbase transactions. In this case,
    // we use a default of 2000 millisatoshis/byte (i.e. 2 satoshi/byte)
        2000
} else {
    // Choose the 50th percentile for sending fees.
    fee_percentiles[50]
};

```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/rust/basic_bitcoin/src/basic_bitcoin/src/bitcoin_wallet.rs#L47C4-L58C7">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
</Tabs>

## Build the transaction

Next, the transaction can be built. The following snippet shows a simplified
version of how to build a transaction that sends `amount` satoshis to the
`dst_address`, and returns the change to the `own_address`.

Since the fee of a transaction is based on its size, the transaction has to be
built iteratively and signed with a mock signer that just adds the respective
size of the signature. Each selected UTXO is used as an input for the
transaction and requires a signature.

<Tabs groupId="language">
<TabItem value="motoko" label="Motoko" default>

```motoko

// Builds a transaction to send the given `amount` of satoshis to the
// destination address.
public func build_transaction(
    own_public_key : [Nat8],
    own_address : BitcoinAddress,
    own_utxos : [Utxo],
    dst_address : BitcoinAddress,
    amount : Satoshi,
    fee_per_vbyte : MillisatoshiPerVByte,
) : async [Nat8] {
    // We have a chicken-and-egg problem where we need to know the length
    // of the transaction in order to compute its proper fee, but we need
    // to know the proper fee in order to figure out the inputs needed for
    // the transaction.
    //
    // We solve this problem iteratively. We start with a fee of zero, build
    // and sign a transaction, see what its size is, and then update the fee,
    // rebuild the transaction, until the fee is set to the correct amount.
    let fee_per_vbyte_nat = Nat64.toNat(fee_per_vbyte);
    Debug.print("Building transaction...");
    var total_fee : Nat = 0;
    loop {
        let transaction =
            Utils.get_ok_except(Bitcoin.buildTransaction(2, own_utxos, [(#p2pkh dst_address, amount)], #p2pkh own_address, Nat64.fromNat(total_fee)), "Error building transaction.");

        // Sign the transaction. In this case, we only care about the size
        // of the signed transaction, so we use a mock signer here for efficiency.
        let signed_transaction_bytes = await sign_transaction(
            own_public_key,
            own_address,
            transaction,
            "", // mock key name
            [], // mock derivation path
            mock_signer,
        );

        let signed_tx_bytes_len : Nat = signed_transaction_bytes.size();

        if((signed_tx_bytes_len * fee_per_vbyte_nat) / 1000 == total_fee) {
            Debug.print("Transaction built with fee " # debug_show(total_fee));
            return transaction.toBytes();
        } else {
            total_fee := (signed_tx_bytes_len * fee_per_vbyte_nat) / 1000;
        }
    }
  };

```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/motoko/basic_bitcoin/src/basic_bitcoin/src/BitcoinWallet.mo#L107">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
<TabItem value="rust" label="Rust">

```rust

// Builds a transaction to send the given `amount` of satoshis to the
// destination address.
async fn build_transaction(
    own_public_key: &[u8],
    own_address: &Address,
    own_utxos: &[Utxo],
    dst_address: &Address,
    amount: Satoshi,
    fee_per_byte: MillisatoshiPerByte,
) -> Transaction {
    // We have a chicken-and-egg problem where we need to know the length
    // of the transaction in order to compute its proper fee, but we need
    // to know the proper fee in order to figure out the inputs needed for
    // the transaction.
    //
    // We solve this problem iteratively. We start with a fee of zero, build
    // and sign a transaction, see what its size is, and then update the fee,
    // rebuild the transaction, until the fee is set to the correct amount.
    print("Building transaction...");
    let mut total_fee = 0;
    loop {
        let transaction =
            build_transaction_with_fee(own_utxos, own_address, dst_address, amount, total_fee)
                .expect("Error building transaction.");

        // Sign the transaction. In this case, we only care about the size
        // of the signed transaction, so we use a mock signer here for efficiency.
        let signed_transaction = sign_transaction(
            own_public_key,
            own_address,
            transaction.clone(),
            String::from(""), // mock key name
            vec![],           // mock derivation path
            mock_signer,
        )
        .await;

        let signed_tx_bytes_len = signed_transaction.serialize().len() as u64;

        if (signed_tx_bytes_len * fee_per_byte) / 1000 == total_fee {
            print(&format!("Transaction built with fee {}.", total_fee));
            return transaction;
        } else {
            total_fee = (signed_tx_bytes_len * fee_per_byte) / 1000;
        }
    }
}


```

<a href="https://github.com/dfinity/examples/blob/6c9e0a6b6d12978869cbf9ea2943452518909728/rust/basic_bitcoin/src/basic_bitcoin/src/bitcoin_wallet.rs#L116">
    <div align="center">View in the full example.</div>
</a>
</TabItem>
</Tabs>

[Learn more about constructing Bitcoin transactions with the Rust Bitcoin Cookbook](https://rust-bitcoin.org/book/intro.html).


## Next steps

[Learn how to sign the transaction.](sign-transactions.mdx)
